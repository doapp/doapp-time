group 'com.doapps'
version "DEV-SNAPSHOT"

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6

repositories {
    jcenter()
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'time'
            from components.java
            version tagname()

            artifact sourceJar { classifier "sources" }
        }
    }

    repositories {
        maven {
            url doappRepositoryUrl
            credentials {
                username doappRepositoryUser
                password doappRepositoryPassword
            }
        }
    }
}

dependencies {
    compile 'joda-time:joda-time:2.3'
    
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
}

//---------- git interactions ------------

ext {
    tagname = {
        def buffer = new ByteArrayOutputStream()

        def result = exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = buffer
        }

        if (result.exitValue != 0)
            throw new GradleException("Unable to find release tag")

        return buffer.toString().trim()
    }
}
